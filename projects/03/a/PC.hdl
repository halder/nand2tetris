// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
        Inc16(in=state, out=add);
        
        // select current state or state incremented by 1
        Mux16(a=state, b=add, sel=inc, out=sel1);
        // select previous selection (state or increment) or set counter to input
        Mux16(a=sel1, b=in, sel=load, out=sel2);
        // select previous selection (state, increment or input) or reset to 0
        Mux16(a=sel2, b=false, sel=reset, out=sel3);
        
        Register(in=sel3, load=true, out=state, out=out);
}